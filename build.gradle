buildscript {
    ext {
        joobyVersion = "3.6.1"
        jupiterVersion = "6.1.0-M1"
        okHttpVersion = "5.3.2"
    }
}

plugins {
    id "application"
    id "io.jooby.openAPI" version "${joobyVersion}"
    id "io.jooby.run" version "${joobyVersion}"
    id "io.spring.dependency-management" version "1.1.7"
    id "com.google.osdetector" version "1.7.3"
    id "com.gradleup.shadow" version "9.3.0"
    id "com.github.ben-manes.versions" version "0.53.0"
    id "com.diffplug.spotless" version "8.1.0"
    id "jacoco"
}

group = "app"
version = "1.0.0"

application {
    mainClass = "com.github.Program"
}

repositories {
    mavenLocal()
    mavenCentral()
}

dependencyManagement {
    imports {
        mavenBom "io.jooby:jooby-bom:$joobyVersion"
    }
}

dependencies {
    annotationProcessor "io.jooby:jooby-apt"

    implementation "io.jooby:jooby-netty:$joobyVersion"
    implementation "io.jooby:jooby-guice:$joobyVersion"
    implementation "io.jooby:jooby-jackson:$joobyVersion"
    implementation "io.jooby:jooby-rxjava3:$joobyVersion"
    implementation 'commons-validator:commons-validator:1.10.1'

    implementation "com.squareup.okhttp3:okhttp:$okHttpVersion"
    implementation "ch.qos.logback:logback-classic:1.5.23"

    testImplementation "org.junit.jupiter:junit-jupiter-api:$jupiterVersion"
    testImplementation "org.junit.jupiter:junit-jupiter-engine:$jupiterVersion"
    testImplementation "org.junit.platform:junit-platform-launcher:$jupiterVersion"
    testImplementation "io.jooby:jooby-test"
    testImplementation "org.mockito:mockito-core:5.21.0"
    testImplementation "com.squareup.okhttp3:mockwebserver:$okHttpVersion"
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport // report is always generated after tests run
}

/** Java debug information: */
tasks.withType(JavaCompile).configureEach {
    options.compilerArgs += ['-parameters',
                             '-Ajooby.incremental=true',
                             '-Ajooby.services=true',
                             '-Ajooby.debug=false']
    options.debug = true
}

shadowJar {
    mergeServiceFiles()
}

joobyRun.dependsOn openAPI
jar.dependsOn openAPI

jacoco {
    toolVersion = "0.8.13"
}

jacocoTestReport {
    // ./gradlew test jacocoTestReport from CLI
    dependsOn test
    group = "Reporting"
    reports {
        xml.required.set(true)
        csv.required.set(true)
        html.required.set(true)
        html.outputLocation.set(layout.buildDirectory.dir("reports/coverage"))
    }
}
