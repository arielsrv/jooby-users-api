buildscript {
    ext {
        joobyVersion = "3.10.0"
        jupiterVersion = "5.13.4"
        okHttpVersion = "5.1.0"
    }
}

plugins {
    id "application"
    id "io.jooby.openAPI" version "${joobyVersion}"
    id "io.jooby.run" version "${joobyVersion}"
    id "io.spring.dependency-management" version "1.1.7"
    id "com.google.osdetector" version "1.7.3"
    id "com.github.johnrengelman.shadow" version "8.1.1"
    id "com.github.ben-manes.versions" version "0.52.0"
    id "com.diffplug.spotless" version "7.2.0"
    id "jacoco"
}

group = "app"
version = "1.0.0"

application {
    mainClass = "com.github.Program"
}

repositories {
    mavenLocal()
    mavenCentral()
}

dependencyManagement {
    imports {
        mavenBom "io.jooby:jooby-bom:$joobyVersion"
    }
}

dependencies {
    annotationProcessor "io.jooby:jooby-apt"

    implementation "io.jooby:jooby-netty:$joobyVersion"
    implementation "io.jooby:jooby-guice:$joobyVersion"
    implementation "io.jooby:jooby-jackson:$joobyVersion"
    implementation "io.jooby:jooby-rxjava3:$joobyVersion"
    implementation "io.jooby:jooby-netty"
    implementation 'commons-validator:commons-validator:1.10.0'

    implementation "com.squareup.okhttp3:okhttp:$okHttpVersion"
    implementation "ch.qos.logback:logback-classic:1.5.18"

    testImplementation "org.junit.jupiter:junit-jupiter-api:$jupiterVersion"
    testImplementation "org.junit.jupiter:junit-jupiter-engine:$jupiterVersion"
    testImplementation "org.junit.platform:junit-platform-launcher:1.13.4"
    testImplementation "io.jooby:jooby-test"
    testImplementation "org.mockito:mockito-core:5.18.0"
    testImplementation "com.squareup.okhttp3:mockwebserver:$okHttpVersion"
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport // report is always generated after tests run
}

/** Java debug information: */
tasks.withType(JavaCompile).configureEach {
    options.compilerArgs += ['-parameters',
                             '-Ajooby.incremental=true',
                             '-Ajooby.services=true',
                             '-Ajooby.debug=false']
    options.debug = true
}

shadowJar {
    mergeServiceFiles()
}

joobyRun.dependsOn openAPI
jar.dependsOn openAPI

jacoco {
    toolVersion = "0.8.13"
}

jacocoTestReport {
    // ./gradlew test jacocoTestReport from CLI
    dependsOn test
    group = "Reporting"
    reports {
        xml.required.set(true)
        csv.required.set(true)
        html.required.set(true)
        html.outputLocation.set(layout.buildDirectory.dir("reports/coverage"))
    }
}
