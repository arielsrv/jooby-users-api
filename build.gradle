buildscript {
    ext {
        joobyVersion = "3.0.5"
    }
}

plugins {
    id "application"
    id "io.jooby.openAPI" version "${joobyVersion}"
    id "io.jooby.run" version "${joobyVersion}"
    id "io.spring.dependency-management" version "1.1.3"
    id "com.google.osdetector" version "1.7.3"
    id "com.github.johnrengelman.shadow" version "8.1.1"
    id "com.github.ben-manes.versions" version "0.47.0"
    id "jacoco"
}

joobyRun {
    mainClass = "com.github.App"
    restartExtensions = ["conf", "properties", "class"]
    compileExtensions = ["java", "kt"]
    port = 8080
    waitTimeBeforeRestart = 500
}

group "app"
version "1.0.0"
mainClassName = "com.github.App"
sourceCompatibility = 17

repositories {
    mavenLocal()
    mavenCentral()
}

dependencyManagement {
    imports {
        mavenBom "io.jooby:jooby-bom:$joobyVersion"
    }
}

dependencies {
    annotationProcessor "io.jooby:jooby-apt"
    implementation "io.jooby:jooby-netty"
    implementation "ch.qos.logback:logback-classic:1.4.11"
    implementation "io.jooby:jooby-guice:$joobyVersion"
    implementation "io.jooby:jooby-jackson:$joobyVersion"
    implementation "io.jooby:jooby-rxjava3:$joobyVersion"
    implementation "com.squareup.okhttp3:okhttp:5.0.0-alpha.11"
    implementation "com.squareup.okio:okio:3.5.0"
    implementation "com.google.guava:guava:32.1.2-jre"
    testImplementation "org.junit.jupiter:junit-jupiter-api:5.10.0"
    testImplementation "org.junit.jupiter:junit-jupiter-engine:5.10.0"
    testImplementation "io.jooby:jooby-test"
    testImplementation "com.squareup.okhttp3:okhttp:4.11.0"
    testImplementation "org.mockito:mockito-core:5.5.0"
}

test {
    useJUnitPlatform()
}

/** Java debug information: */
tasks.withType(JavaCompile).configureEach {
    options.compilerArgs += [
            '-parameters',
            '-Ajooby.incremental=true',
            '-Ajooby.services=true',
            '-Ajooby.debug=false'
    ]
    options.debug = true
}


shadowJar {
    mergeServiceFiles()
}

joobyRun.dependsOn openAPI

jar.dependsOn openAPI

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport // report is always generated after tests run
}

jacoco {
    toolVersion = "0.8.9"
}

jacocoTestReport {
    // ./gradlew test jacocoTestReport from CLI
    dependsOn test
    group = "Reporting"
    reports {
        xml.required.set(true)
        csv.required.set(true)
        html.destination file("${layout.getBuildDirectory().get()}/reports/coverage")
    }

    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: ['**/github/App.*',
                                        '**/github/core/*',
            ])
        }))
    }
}
